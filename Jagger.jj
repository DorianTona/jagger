// Options for JavaCC.
options { LOOKAHEAD=1; FORCE_LA_CHECK=true; }

// Fonction principale
PARSER_BEGIN(Jagger)

import java.util.ArrayList;
import java.util.HashMap;

public class Jagger
{
    static PrettyPrinter pp = new PrettyPrinter();
    static Eval ev = new Eval();
}


PARSER_END(Jagger)

// Characters to ignore.
SKIP: { " " | "\r" | "\t" | "\n" }

// Token specifications.
TOKEN:
{
    < NUMBER: (<DIGIT>)+ ("." (<DIGIT>)*)? >			// A decimal number.
|   < DIGIT: ["0"-"9"] >								// A decimal digit.
//|   < EOL: "\n" >										// End of line.
|   < STRING: "\"" ((~["\n", "\r", "\""])*)? "\"" >		// string

|	< PRINT: "print" >

|	< IF: "if" >
|	< THEN: "then" >
|	< ELSE: "else" >

|	< LET: "let" >
|	< IN: "in" >
|	< END: "end" >
|	< VAR: "var" >
|   < VARIABLE: ["a"-"z", "A"-"Z"](["a"-"z", "A"-"Z","0"-"9"])* >    // variable
}

// Main loop: read expressions on a line until end of file.
//     mainloop → (statement)* <EOF>
void mainloop():
{ Exp a; }
{
    ( a=scope() )* <EOF>
}

//     scope → <LET> declaration <IN> comparison (,comparison)* <END>
Exp scope():
{ Exp a, s; }
{
    <LET>
        s = declaration()
    <IN>
        s = assignation(s) ("," s = assignation(s))*
    <END>
    { 
        pp.print(s);
        System.out.println("\nResult :");
        ev.print(s);
        ((Scope)s).exit();
        return s;
    }
}

Exp assignation(Exp s):
{
    Exp a; 
    Token t;
}
{
    (
      a = scope() { ((Scope)s).addInstruction(a); }
    | <PRINT> "(" a=comparison() ")" { ((Scope)s).addInstruction( new Print(a)); }
    | t=<VARIABLE> "," a=comparison() { ((Scope)s).changeValue(t.toString(), a); }
    ) { return s; }
}

//     declaration → (var <VARIABLE> := comparison)*
Scope declaration():
{
    Token t;
    Exp e;
    Scope s = new Scope();
}
{
    ( <VAR> t=<VARIABLE> ":=" e=comparison() { s.addDeclaration(t.toString(), e); } )*
    ("," <VAR> t=<VARIABLE> ":=" e=comparison() { s.addDeclaration(t.toString(), e); } )*
    { return s; }
}


// Comparison operators
// C -> E ('=='E | '<>'E | '>'E | '<'E | '>='E | '<='E)*
Exp comparison():
{ Exp a,b; }
{
    a=expression()
    (
      "=="  b=expression()  { a = new Equal(a,b); }
    | "<>"  b=expression()  { a = new NonEqual(a,b); }
    | ">"   b=expression()  { a = new Sup(a,b); }
    | "<"   b=expression()  { a = new Inf(a,b); }
    | ">="  b=expression()  { a = new SupEqual(a,b); }
    | "<="  b=expression()  { a = new InfEqual(a,b); }
    )? { return a; }
}

// Expression (the axiom).
// E -> T ('+'T | '-'T)*
Exp expression():
{ Exp a,b; }
{
    a=term()
    (
      "+" b=term()  { a = new Add(a,b); }
    | "-" b=term()  { a = new Sub(a,b); }
    )* { return a; }
}


// Term.
// T -> U ('*'T | '/'T)*
Exp term():
{ Exp a,b; }
{
    a=unary()
    (
      "*" b=term() { a = new Mul(a,b); }
    | "/" b=term() { a = new Div(a,b); }
    )* { return a; }
}

// Unary operator.
// U -> ('*'T | '/'T | T)*
Exp unary():
{ Exp a; }
{
      "-" a=ternary() { return a = new UnNeg(a); }
    | ("+" a=ternary() | a=ternary()) { return a; }
}

//ternary operators
// T -> ('if' C 'then' C 'else' C | F)
Exp ternary():
{ Exp a,b,c; }
{   
    (
      <IF> a=comparison() <THEN> b=comparison() <ELSE> c=comparison() {  a = new Ins(a,b,c); }
    | a=factor()
    )+ { return a; }
}

// Factor of an expression.
// F -> <NUMBER> | <STRING> | <VARIABLE> | "(" E ")"
Exp factor():
{ Token t; Exp e;}
{
      t=<NUMBER> { return new Num(Double.parseDouble(t.toString())); }
    | t=<STRING> { return new Chaine(t.toString()); }
    | t=<VARIABLE> { return new Variable(t.toString()); }
    | "(" e=comparison() ")" { return e; }
}
