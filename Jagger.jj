// Options for JavaCC.
options { LOOKAHEAD=2; FORCE_LA_CHECK=true; }

// Fonction principale
PARSER_BEGIN(Jagger)

import java.util.ArrayList;
import java.util.HashMap;

public class Jagger
{
    static Scope parent = null;

    public static void main(String args[]) throws ParseException
    {
        (new Jagger(System.in)).mainloop();
    }
}


PARSER_END(Jagger)

// Characters to ignore.
SKIP: { " " | "\r" | "\t" | "\n" }

// Token specifications.
TOKEN:
{
    //Keywords
    < PRINT: "print" >
|   < LRB: "(" >                                              // Left Round Bracket
|   < RRB: ")" >                                              // Right Round BRacket

|   < IF: "if" >
|   < THEN: "then" >
|   < ELSE: "else" >

|   < LET: "let" >
|   < ASSIGN: ":=" >
|   < FASSIGN: "=" >
|   < VAR: "var" >
|   < IN: "in" >
|   < COMMA: "," >
|   < END: "end" >

|   < FUNCTION: "function" >
|   < SEMICOLON: ";" >

    //Types
|   < #DIGIT: ["0"-"9"] >                                     // A decimal digit.
|   < NUMBER: (<DIGIT>)+ ("." (<DIGIT>)*)? >                  // A decimal number.
|   < STRING: "\"" ((~["\n", "\r", "\""])*)? "\"" >           // string
|   < ID: ["a"-"z", "A"-"Z"](["a"-"z", "A"-"Z","0"-"9"])* >   // variable
}

//common examples to try out
//let var i := 10 in print(i), let var i := i * i in print(i) end, print(i) end
//let var a:=1 in a:=2, print(a) end
//let var a:=1 function b()=2 in print(a), a:=b(), print(a) end
//let var a:=1 function a(a,b)=2 in print(a), a:=a(0,0), print(a) end
//let function a(a)=print(a*2) in a(333) end
//let function a(a)=a:=a var a:=1 in a(2), print(a) end
//let function a()=(print(1);print(2)) in a() end
//let in print(let in 1 end + let in 2 end) end

// Main loop: read expressions on a line until end of file.
//  mainloop → (print | scope)* <EOF>
void mainloop():
{ Exp e; }
{
    ( ( e=print() | e=scope() )
    { new PrettyPrinter(e); System.out.println(); new Eval(e); parent=null; } 
    )* <EOF>
}

//possible instructions
//  statement → print | functionCall | comparison
Exp statement():
{ Token t; Exp e; }
{
    ( t=<ID> <ASSIGN> e=comparison() { e=new Assignment(t.toString(), e, parent); }
    | e=print()
    | e=functionCall() 
    | e=comparison()
    )
    { return e; }
}

//has its own function for convenience
//  print → print(comparison)
Exp print():
{ Exp e; }
{
    <PRINT> <LRB> e=comparison() <RRB> { return new Print(e); }
}

//  scope → let (declaration)* in instruction (, instruction)* end
Exp scope():
{ Exp e; Scope s = new Scope(parent); parent=s; }
{
    <LET>
        (declaration())*
    <IN>
        e=statement() { s.addInstruction(e); } (<COMMA> e=statement() { s.addInstruction(e); })*
    <END>
    { return s; }
}

//  declaration → (var <ID> := comparison)*
void declaration():
{ Token t; Exp e; Function f; }
{
    (
      <VAR> t=<ID> <ASSIGN> e=comparison() { parent.addDeclaration(t.toString(), e); }
    | <FUNCTION> f=functionDeclaration() { parent.addFunction(f); }
    ) 
}

//  function → funtion <ID> ( <ID>(, <ID>)*)? ) = statement
Function functionDeclaration():
{ Token t; Exp e; Function f = new Function(parent); }
{
    t=<ID> {f.name = t.toString();} <LRB> (e=variable() {f.addParam(e);}(<COMMA> e=variable() {f.addParam(e);})*)? <RRB> <FASSIGN> (
      e=statement() {f.addIns(e);} 
    | <LRB> (e=statement() {f.addIns(e);} (<SEMICOLON> e=statement() {f.addIns(e);} )* <RRB> ))
    { return f; }
}

//  function → funtion <ID> <LRB> <ID>(, <ID>)*)? <RRB>
Function functionCall():
{ Token t; Exp e; Function f = new Function(parent); }
{
    t=<ID> <LRB> (e=comparison() {f.addParam(e);}(<COMMA> e=comparison() {f.addParam(e);})*)? <RRB>
    { f.name = t.toString(); return f; }
}

// Comparison operators. Separated to avoid 1>0>1 true (because 1>0=1)
// C -> E ('=='E | '<>'E | '>'E | '<'E | '>='E | '<='E)*
Exp comparison():
{ Exp a,b; }
{
    
    a=expression()
    (
      "=="  b=expression()  { a = new Equal(a,b); }
    | "<>"  b=expression()  { a = new NonEqual(a,b); }
    | ">"   b=expression()  { a = new Sup(a,b); }
    | "<"   b=expression()  { a = new Inf(a,b); }
    | ">="  b=expression()  { a = new SupEqual(a,b); }
    | "<="  b=expression()  { a = new InfEqual(a,b); }
    )?
    { return a; }
}

// Expression (the axiom).
// E -> T ('+'T | '-'T)*
Exp expression():
{ Exp a,b; }
{
    a=term()
    (
      "+" b=term()  { a = new Add(a,b); }
    | "-" b=term()  { a = new Sub(a,b); }
    )* { return a; }
}


// Term.
// T -> U ('*'T | '/'T)*
Exp term():
{ Exp a,b; }
{
    a=unary() (
      "*" b=term() { a = new Mul(a,b); }
    | "/" b=term() { a = new Div(a,b); }
    )*
    { return a; }
}

// Unary operator.
// U -> ('*'T | '/'T | T)*
Exp unary():
{ Exp a; }
{
    ( "-" a=ternary()   { a = new UnNeg(a); }
    | ("+")? a=ternary()
    ) { return a; }
}

//ternary operators
// T -> ('if' C 'then' C 'else' C | F)
Exp ternary():
{ Exp a,b,c; }
{   
    ( a=scope()
    | a=functionCall()
    | <IF> a=comparison() <THEN> b=comparison() <ELSE> c=comparison() { a = new Ins(a,b,c); } 
    | a=factor()
    )+ { return a; }
}

// Factor of an expression.
// F -> <NUMBER> | <STRING> | <ID> | "(" E ")"
Exp factor():
{ Token t; Exp e;}
{
    ( t=<NUMBER> { e = new Num(Double.parseDouble(t.toString())); }
    | <LRB> e=comparison() <RRB>
    | t=<STRING> { e = new Chaine(t.toString()); }
    | e=variable()
    ) { return e; }
}

//has its own function for convenience
Exp variable():
{ Token t; }
{
    t=<ID> { return new Variable(t.toString()); }
}
